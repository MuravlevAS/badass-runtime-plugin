import groovy.text.SimpleTemplateEngine
import org.kohsuke.github.*

buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'org.kohsuke:github-api:1.94'
    }
}

plugins {
    id 'java-gradle-plugin'
    id 'com.github.johnrengelman.plugin-shadow' version '2.0.3'
    id 'groovy'
    id 'maven-publish'
    id "com.gradle.plugin-publish" version "0.9.10"
    id "com.github.ethankhall.semantic-versioning" version "1.1.0"
    id "com.github.ben-manes.versions" version "0.13.0"
    id 'com.github.jk1.dependency-license-report' version '0.3.8'
    id "com.github.hierynomus.license" version "0.14.0"
    id "org.asciidoctor.convert" version "2.3.0"
    id "org.ajoberstar.git-publish" version "2.1.1"
}

project.version.with {
    major = badassRuntimePluginVersionMajor as int
    minor= badassRuntimePluginVersionMinor as int
    patch = badassRuntimePluginVersionPatch as int
    if (project.hasProperty('badassRuntimePluginVersionLabel')) {
        preRelease = badassRuntimePluginVersionLabel
    }
    releaseBuild = Boolean.valueOf(badassRuntimePluginReleaseBuild)
}
ext.badassRuntimePluginVersion = project.version as String
ext.badassRuntimePluginTag = Boolean.valueOf(badassRuntimePluginReleaseBuild) ? "v$ext.badassRuntimePluginVersion" : 'master'

group = 'org.beryx'
version = badassRuntimePluginVersion

ext.asmVersion = '8.0.1'

repositories {
    jcenter()
    mavenCentral()
}

sourceCompatibility = 1.8

def defaultEncoding = 'UTF-8'
[compileJava, compileTestJava]*.options*.encoding = defaultEncoding

[compileGroovy, compileTestGroovy]*.options*.encoding = defaultEncoding
[compileGroovy, compileTestGroovy]*.groovyOptions*.encoding = defaultEncoding

task licenseCheckGroovy(type: com.hierynomus.gradle.license.tasks.LicenseCheck) {
    source = fileTree(dir: "src").include("**/*.groovy")
}
tasks.license.dependsOn licenseCheckGroovy

license {
    header rootProject.file("license-header.txt")
    skipExistingHeaders true
    ignoreFailures false
    excludes (['**/*.properties', '**/*.txt', '**/hello*/'])
}

configurations {
    compile {transitive = true}
}

dependencies {
    shadow gradleTestKit()
    shadow localGroovy()

    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'

    compile "org.ow2.asm:asm:$asmVersion"
    compile "org.ow2.asm:asm-commons:$asmVersion"
    compile "org.ow2.asm:asm-tree:$asmVersion"
    compile "org.ow2.asm:asm-util:$asmVersion"
    compile "org.ow2.asm:asm-analysis:$asmVersion"

    testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude module: 'groovy-all'
    }
    testCompile 'cglib:cglib-nodep:3.2.4'
    testCompile 'org.objenesis:objenesis:2.4'
    testCompile('com.athaydes:spock-reports:1.2.13') { transitive = false}

    asciidoctor 'com.bmuschko:asciidoctorj-tabbed-code-extension:0.2'
}

gradlePlugin {
    plugins {
        runtime {
            id = 'org.beryx.runtime'
            implementationClass = 'org.beryx.runtime.RuntimePlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/beryx/badass-runtime-plugin/'
    vcsUrl = 'https://github.com/beryx/badass-runtime-plugin'
    description = 'A Gradle plugin that assembles your modules into a custom runtime image'
    tags = ['runtime', 'jlink', 'jpms', 'image']

    plugins {
        runtime {
            id = 'org.beryx.runtime'
            displayName = 'Badass Runtime Plugin'
        }
    }
}


jar {
    manifest {
        attributes 'Implementation-Title': "${project.archivesBaseName}",
                'Implementation-Version': badassRuntimePluginVersion
    }
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
}

asciidoctor {
    sourceDir = file("doc")
    sources {include 'index.adoc'}
    logDocuments = true
    attributes 'source-highlighter': 'coderay',
            'coderay-linenums-mode': 'table',
            'project-version': version,
            icons: 'font',
            imagesdir: 'img',
            'data-uri': '',
            linkattrs: true,
            linkcss: true,
            'git-tag': badassRuntimePluginTag,
            'blob-root': "https://github.com/beryx/badass-runtime-plugin/blob/$badassRuntimePluginTag"
}

gitPublish {
    repoUri = 'https://github.com/beryx/badass-runtime-plugin.git'
    branch = 'gh-pages'

    contents {
        def pgType = project.hasProperty('ghPageType') ? ghPageType : 'latest'
        if(pgType == 'init') {
            from file("ghpages")
        } else if(pgType == 'list') {
            from file("build/release-list")
        } else {
            from file("${asciidoctor.outputDir.path}/html5")
            from file("build/docs")
        }
        def docDir = Boolean.valueOf(badassRuntimePluginReleaseBuild) ? 'releases' : 'snapshots'
        if(pgType == 'init') {
            into "."
        } else if(pgType == 'list') {
            into "."
            preserve {
                include '**'
                exclude "releases.md"
            }
        } else if(pgType == 'version') {
            gitPublishPush.enabled = (docDir != 'snapshots')
            into "$docDir/$badassRuntimePluginVersion"

            preserve {
                include '**'
                exclude "$docDir/$badassRuntimePluginVersion"
            }
        } else {
            into "$docDir/latest"

            preserve {
                include '**'
                exclude "$docDir/latest"
            }
        }
    }
}

task "update-release-list" {
    doLast {
        def docBaseUrl = 'http://badass-runtime-plugin.beryx.org'
        updateReleaseList(docBaseUrl)
    }
}

def updateReleaseList(String docBaseUrl) {
    List<String> releases = getReleases()
    def markdown = getReleasesMarkdown(releases, docBaseUrl)
    def releaseListDir = new File('build/release-list')
    releaseListDir.mkdirs()
    new File(releaseListDir, 'releases.md').write(markdown)
}

def getReleases() {
    GitHub gitHubApi = GitHub.connectUsingPassword(System.getenv('GRGIT_USER') ?: '', '')
    def releases = gitHubApi.getOrganization("beryx")
            .getRepository("badass-runtime-plugin")
            .getDirectoryContent("releases", "gh-pages")*.name
    releases.removeAll { !it || it == 'latest' }
    releases.reverse()
}

def getReleasesMarkdown(List<String> releases, String docBaseUrl) {
    def releasesTemplate = '''
            ## List of all releases ##

            **Latest snapshot**
            - [documentation]($docBaseUrl/snapshots/latest)
            - [groovydoc]($docBaseUrl/snapshots/latest/groovydoc)

            <% releases.each { %>**Release $it**
              - [documentation]($docBaseUrl/releases/$it)
              - [groovydoc]($docBaseUrl/releases/$it/groovydoc)

            <% } %>
        '''.stripIndent()

    def engine = new SimpleTemplateEngine()
    def template = engine.createTemplate(releasesTemplate)
    def binding = [
            docBaseUrl: docBaseUrl,
            releases  : releases
    ]

    template.make(binding).toString()
}
